Pie {
    pieFile = (topLevel | blankLine)*

    topLevel = environmentSpec | environmentalDef | requestDef

    environmentalDef (Header or variable declaration)
    = headerDef | variableDef

    headerDef (Header)
    = identifier separator<":"> value

    variableDef (Variable declaration)
    = "$" identifier separator<"="> value

    environmentSpec (Environment)
    = "@" identifier separator<":"> comment? inEnvDefs

    inEnvDefs
    = (inEnvDef | blankLine)*

    inEnvDef (Indented Header or variable)
    = eos indent environmentalDef

    requestDef (Request)
    = requestMethod requestPath #eos requestHeaders? requestBody?

    requestMethod
    = methodName space+

    // TODO allow trailing comments?
    requestPath
    = value

    requestHeaders
    = headerDef+ eos

    methodName (HTTP Request Method)
    = upper+

    requestBody (Any series of non-blank lines)
    = (~emptyLine value eos)+

    comment (A comment)
    = space* "#" value

    commentAtEol (A comment)
    = comment eol

    eol (End of line)
    = "\n" | "\r"

    eos (End of statement)
    = eol | commentAtEol | end

    blankLine = eol space* comment?
    emptyLine = eol space* (eol | commentAtEol)

    separator<token> = spaces token spaces
    value = (~eol any)+
    identifier = (alnum | "-")+
    indent = "\t" | "  "+
    space := (" " | "\t")+
}
